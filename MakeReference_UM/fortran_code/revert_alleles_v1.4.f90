      program revert_alleles
!--------------------------------------------------------------------------------------
!     Program revert_alleles changes allele identities in output files produced by
!     my modified versions of the scripts for SNP2HLA and MakeReference (Jia 2013;
!     SNP2HLA software 2016). 
!
!     The original version (v1.0.3) of SNP2HLA and MakeReference use P/A and one 
!     letter amino acid codes for HLA gene and amino acid alleles, respectively.  
!     This causes no problem for v3 of Beagle that is used by these original 
!     scripts for phasing and imputation.  However, my modified versions of these 
!     scripts employ the much faster and more accurate v4.1 of Beagle, which will 
!     only accept combinations of the {A,C,G,T,N} character set.  To deal with this 
!     incompatibility, I hacked the alleles using multiple character combinations
!     of ACTG in my programs for creating the vcf input file for beagle 4.1 (i.e., 
!     beagle2vcf).  This of course means that the output files after running
!     Beagle 4.1 (*.bgl.vcf, *.bgl.gprobs, *.dosage for SNP2HLA script, *.bgl.vcf
!     for MakeReference script) also have these hacked alleles, which need to
!     be reverted to the original identities.
!     
!     Two input files are required.  The first is a phased genotype file in
!     vcf 4.1 format generated by Beagle 4.1, which may or may not include
!     posterior genotype probabilities and imputation qualities.  The second
!     is a Plink *.bim file holding information for the markers in the vcf
!     file (both files need to have markers in same order), including the
!     original pre-hacked identities for the reference and alternate alleles.
!     If the user specifies that files generated by the SNP2HLA rather than
!     the MakeReference script are being processed, then two additional required
!     input files are a posterior genotype probability file and a genotype
!     dosage file.
!
!     Upon exit, the input files (vcf file and also possibly genotype probability 
!     and genotype dosage files) have their variant allele identities reverted
!     to their original pre-haced state, but are otherwise identical to the
!     input files.
!
!     To compile this with gfortran for linux executables, the Windows system
!     commands for 'copy' and 'del' need to be changed to the equivalent
!     Linux system commands 'cp' and 'rm'.
!                                                                      
!     Written by Philip Stuart in standard Fortran 2003.                 
!                                                                      
!     Revision History:												 
!                                                                      
!     v 1.0  05/10/16   First version.
!
!     v 1.1  05/24/16   Added ability to revert alleles for the posterior genotype
!                       probability file and the genotype dosage file that are
!                       generated by the MakeReference Script. 
!
!     v 1.2  09/30/16   Increased permissible maximum length of fields in the vcf 
!                       file to 100 characters and of filenames to 250 characters.
!
!     v 1.3  07/18/19   Increased permissible maximum length of any line in the input
!                       file, read into character variable 'string', from 250,000
!                       to 5 million characters.  This change was prompted by finding
!                       a variant imputed for the Franke+23BKT reference panel and
!                       the 11,675 sample exomechip dataset whose line exceeded
!                       250,000 characters, prompting an error message when using
!                       bcftools to merge vcf files for all 8 European datasets.
!
!     v 1.4  08/28/19   Increased length of character scalars fnvcf, fngprobs,
!                       fndosage and fnref from 250, arg from 251 to 1001, command
!                       from 270 to 1030, length of character array fields from
!                       100 to 200 and its dimension from 70000 to 500000.
!
!     References:
!
!     Jia X, Han B, Onengut-Gumuscu S, Chen W-M, Concannon PJ, Rich SS, Raychaudhuri S, 
!        de Bakker PIW (2013) Imputing amino acid polymorphisms in human leukocyte
!        antigens. PLoS One 8(6) e64683.
!
!     SNP2HLA software, version 1.0.3, downloaded in May 2016 from 
!        https://www.broadinstitute.org/impg/snp2hla
!--------------------------------------------------------------------------------------
      implicit none

      integer i,j,item,ioerr, &
              nskip,nmarker,nitem, &
              script_opt
      character(1) accept,atmp
      character(1), allocatable :: a1(:),a2(:)
      character(35) script_opts(2)
      character(200) fields(500000)
      character(1000) fnvcf,fngprobs,fndosage,fnref
      character(1001) arg
      character(1030) command
      character(5000000) string
      logical exist
      
!     Initialize program parameters to default values

      script_opt=1
      script_opts(1)='(1) SNP2HLA_new.csh'
      script_opts(2)='(2) MakeReference_new.csh'
      fnvcf='test.vcf'
      fnref='ref.bim'
      fngprobs='test.gprobs'
      fndosage='test.dosage'
      accept=''

!     Query user for starting parameters.  If no arguments on command line, bring up
!     menu for interactive input.  Otherwise, parse command line Unix-style to determine
!     starting parameters.

      if (command_argument_count()>0) then
        do i=1,command_argument_count(),2
          call get_command_argument(i,arg)
          select case (arg)
            case('-script_opt')
              call get_command_argument(i+1,atmp)
              read(atmp,'(i1)') script_opt
            case('-fnvcf')
              call get_command_argument(i+1,fnvcf)
	          inquire(file=fnvcf,exist=exist)
              if (.not.exist) then
                write(*,'(/a,a,a)') ' Input file ',trim(fnvcf),' not in directory'
                stop
              end if
            case('-fnref')
              call get_command_argument(i+1,fnref)
	          inquire(file=fnref,exist=exist)
              if (.not.exist) then
                write(*,'(/a,a,a)') ' Input file ',trim(fnref),' not in directory'
                stop
              end if
            case('-fngprobs')
              call get_command_argument(i+1,fngprobs)
	          inquire(file=fngprobs,exist=exist)
              if (.not.exist) then
                write(*,'(/a,a,a)') ' Input file ',trim(fngprobs),' not in directory'
                stop
              end if
            case('-fndosage')
             call get_command_argument(i+1,fndosage)
	         inquire(file=fndosage,exist=exist)
             if (.not.exist) then
               write(*,'(/a,a,a)') ' Input file ',trim(fndosage),' not in directory'
               stop
             end if
            case default
              write (*,'(a,a,/)') 'Unrecognized command-line option: ', arg
              stop
          end select
        end do
      else

!       Print menu on console allowing user to enter input information 
!       and select program options.  Keep cycling through menu until 
!       user indicates (with a '21') that all selections have been made.

        do while (item/=21)

          nskip=20
          write(*,9110) script_opts(script_opt),fnvcf(1:35),fnref(1:35)
          if (script_opt==1) then
            nskip=nskip-2
            write(*,9115) fngprobs(1:35),fndosage(1:35)
          end if
          write(*,9190) accept

9110      format (' 1  SCRIPT THAT GENERATED INPUT FILES (1-2)',T45,A/ &
                  ' 2  SNP2HLA IMPUTED VCF FILE',T45,A/ &	
                  ' 3  REFERENCE PANEL *.BIM FILE',T45,A)
9115      format (' 4  SNP2HLA IMPUTED GPROBS FILE',T45,A/ & 
                  ' 5  SNP2HLA IMPUTED DOSAGE FILE',T45,A)
9190      format ('21  ACCEPT PROGRAM',T45,A1)

          do i=1,nskip
            write(*,*)
          end do
          write(*,9195,advance='NO')
9195      format (' CHOOSE AN ITEM [1.....21]: ')

!         Read in user's entry for selected menu item.

           read(*,*) item
           select case (item)
           case (1)
             write (*,9201,ADVANCE='NO')
9201         format (' Script that generated input files (1-2): ')
             read(*,*) script_opt
             do while (script_opt/=1.and.script_opt/=2)
               write (*,9201,ADVANCE='NO')
               read(*,*) script_opt
             end do
           case (2)
             write (*,9202,ADVANCE='NO') 
9202         format (' Name of SNP2HLA imputed vcf file: ')
             read (*,'(a)') fnvcf
	         inquire(FILE=fnvcf,EXIST=EXIST)
	         do while (.not.exist)
	           write (*,9202,ADVANCE='NO')
	           read (*,'(a)') fnvcf
	           inquire(FILE=fnvcf,EXIST=EXIST)
	         end do
           case (3)
             write (*,9203,ADVANCE='NO') 
9203         format (' Name of reference panel plink *.bim file: ')
             read (*,'(a)') fnref
	         inquire(FILE=fnref,EXIST=EXIST)
	         do while (.not.exist)
	           write (*,9203,ADVANCE='NO')
	           read (*,'(a)') fnref
	           inquire(FILE=fnref,EXIST=EXIST)
	         end do
           case (4)
             write (*,9204,ADVANCE='NO') 
9204         format (' Name of SNP2HLA imputed genotype probability file: ')
             read (*,'(a)') fngprobs
	         inquire(FILE=fngprobs,EXIST=EXIST)
	         do while (.not.exist)
	           write (*,9204,ADVANCE='NO')
	           read (*,'(a)') fngprobs
	           inquire(FILE=fngprobs,EXIST=EXIST)
	         end do
           case (5)
             write (*,9205,ADVANCE='NO') 
9205         format (' Name of SNP2HLA imputed dosage file: ')
             read (*,'(a)') fndosage
	         inquire(FILE=fndosage,EXIST=EXIST)
	         do while (.not.exist)
	           write (*,9205,ADVANCE='NO')
	           read (*,'(a)') fndosage
	           inquire(FILE=fndosage,EXIST=EXIST)
	         end do
           case (21)
             exit
           end select
        end do
      endif

      write(*,'(/A//)') ' revert_alleles v 1.4'
      
!     Open reference panel *.bim file.  First determine number of markers.
!     Then rewind and read in all allele pairs for each biallelic marker.
      
      open (unit=15,file=fnref)
      
      nmarker=0
      do
        read(15,*,iostat=ioerr) atmp
        if (ioerr<0) exit
        nmarker=nmarker+1
      end do
      
      rewind(15)
      
      allocate (a1(nmarker),a2(nmarker))
      do i=1,nmarker
        read(15,*) atmp,atmp,atmp,atmp,a1(i),a2(i)
      end do
      
      close(15)
      
!     Open vcf input file.  Write all header lines to temporary file.
      
      open (unit=15,file=fnvcf)
      open (unit=17,file='temp.out')
      
      do
        read(15,'(a)',iostat=ioerr) string
        if (ioerr<0) exit
        write(17,'(a)') trim(string)
        if(string(1:6)=='#CHROM') exit
      end do
      
!     For remaining lines of vcf input file, read in each line, tokenize based on 
!     tab-delimiters, and write items of tokenized string to temporary file with tab
!     delimiters, substituting reverted allele IDs from reference file for those in original 
!     vcf file.  Then copy temporary file over original vcf file.
      
      do i=1,nmarker
        read(15,'(a)') string       
        call tokenize (string,achar(9),fields,70000,nitem)
!       call parse (string,achar(9),fields,nitem)
        fields(4)=a1(i)
        fields(5)=a2(i)
        write(17,'(*(g0,:,"'//achar(9)//'"))') (trim(fields(j)),j=1,nitem)
      end do
      
      close(15); close(17)
      
      write(command,'(a,a,a,a)') 'cp temp.out ','"',trim(fnvcf),'"'
      call execute_command_line (command)
      
      if (script_opt==1) then
      
!       Open genotype probabilities file. File holds one header line, followed
!       by one record for each marker.  Read all records as strings, tokenize
!       based on space delimiters, and write items of tokenized string to
!       temporary file, substituting reverted allele IDs from reference file for
!       those in the original gprobs file.  Then copy temporary file over original
!       genotypes probability file.
      
        open (unit=15,file=fngprobs)  
        open (unit=17,file='temp.out')

        read(15,'(a)') string
        call tokenize (string," ",fields,500000,nitem)
        write(17,'(*(g0,:,1x))') (trim(fields(j)),j=1,nitem)
      
        do i=1,nmarker
          read(15,'(a)') string       
          call tokenize (string," ",fields,500000,nitem)
          fields(2)=a1(i)
          fields(3)=a2(i)
          write(17,'(*(g0,:,1x))') (trim(fields(j)),j=1,nitem)
        end do
      
        close(15); close(17)

        write(command,'(a,a,a,a)') 'cp temp.out ','"',trim(fngprobs),'"'
        call execute_command_line (command)
      
        close(15); close(17)
      
!       Open allele dosage file.  There are no header lines, but just
!       one record for each marker.  Tokenize each line based on tab delimiters, 
!       write to teomporary file using tab delimiters, then copy temporary file over 
!       original dosage file.
      
        open (unit=15,file=fndosage)
        open (unit=17,file='temp.out')
      
        do i=1,nmarker
          read(15,'(a)') string       
          call tokenize (string,achar(9),fields,70000,nitem)
          fields(2)=a1(i)
          fields(3)=a2(i)
          write(17,'(*(g0,:,"'//achar(9)//'"))') (trim(fields(j)),j=1,nitem)
        end do
      
        close(15); close(17)
      
        write(command,'(a,a,a,a)') 'cp temp.out ','"',trim(fndosage),'"'
        call execute_command_line (command)
        
      end if
      
!     delete temporary file
      
      call execute_command_line ('rm temp.out')
      
      end program revert_alleles
!
!
      subroutine tokenize(string,delimiter,array,nmax,n)
!     -------------------------------------------------------------------------
!      This subroutine tokenizes a character string by searching for all
!      occurrences of a specified delimiter (i.e., tab, space, comman, etc.).
!      Upon return the first n elements of vector array hold all delimiter-
!      separated substrings within the original string.
!
!      This routine was modified from code at rosettacode.org:
!      https://www.rosettacode.org/wiki/Tokenize_a_string#Fortran
!-----------------------------------------------------------------------------
      
      integer pos1,pos2,n,nmax,length
      character(1) delimiter
      character(200) array(nmax)
      character(5000000) string
      
      pos1=1; n=0
      length=len_trim(string)
      do
        pos2=index(string(pos1:length),delimiter)
        if (pos2==0) then
          n=n+1
          array(n)=string(pos1:length)
          exit
        end if
        n=n+1
        array(n)=string(pos1:pos1+pos2-2)
        pos1=pos2+pos1
      end do
      
    end subroutine tokenize

       
